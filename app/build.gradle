plugins {
    id 'com.android.application'
    id 'kotlin-android'
//    id 'com.google.gms.google-services'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion(31)
    compileSdk 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.goen.daiku"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField('String', 'FIREBASE_PROJECT_ID', '"' + System.getenv("FIREBASE_PROJECT_ID") + '"')
        buildConfigField('String', 'FIREBASE_APPLICATION_ID', '"' + System.getenv("FIREBASE_APPLICATION_ID") + '"')
        buildConfigField('String', 'FIREBASE_API_KEY', '"' + System.getenv("FIREBASE_API_KEY") + '"')
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    signingConfigs {
        dev {
            storeFile rootProject.file('dev-release.keystore')
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
        prd {
            storeFile rootProject.file('prd-release.keystore')
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }

    buildTypes {

        debug {
            jniDebuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        flavorDimensions "env"
        dev {
            if (rootProject.file('dev-release.keystore').exists()) {
                signingConfig signingConfigs.dev
            }
            dimension "env"

        }
        prd {
            if (rootProject.file('prd-release.keystore').exists()) {
                signingConfig signingConfigs.prd
            }
            dimension "env"

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    composeOptions {
        kotlinCompilerVersion '1.5.31'
        kotlinCompilerExtensionVersion '1.0.5'
    }
}

dependencies {
    implementation project(":auth")
    implementation project(":home")
    implementation project(":goal")
    implementation project(":domain")
    implementation project(":account")
    implementation project(":process")
    implementation project(":processHistory")
    implementation project(":favorite")
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-auth-ktx:21.0.1'

}